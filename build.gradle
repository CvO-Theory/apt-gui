apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = '1.7'

repositories {
    mavenCentral()
}

// Build apt before compiling Java code
ant.importBuild('submodules/apt/build.xml') { antTargetName ->
    'apt_' + antTargetName
}
eclipseClasspath.dependsOn apt_jar
compileJava.dependsOn apt_jar
clean.dependsOn apt_clean

dependencies {
	// APT dependencies
	compile files('submodules/apt/apt.jar')

	// APT-GUI dependencies
	compile 'com.google.inject:guice:4.0'
	compile 'com.google.guava:guava:19.0'
	compile('org.apache.xmlgraphics:batik-dom:1.8'){
		exclude module: 'xalan'
		exclude module: 'batik-css'
		exclude module: 'xml-apis-ext'
		exclude module: 'xml-apis'
	}
	compile('org.apache.xmlgraphics:batik-svggen:1.8') {
		exclude module: 'xml-apis'
	}
	testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.1.0'
    testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
}

eclipse {
	classpath {
		downloadSources = true
		downloadJavadoc = true
	}
}

jar {
    manifest {
        attributes 'Main-Class': 'uniol.aptgui.Main'
    }
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}
